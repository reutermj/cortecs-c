"Contains code related to the Cortecs programming language"
module(name = "cortecs", version = "2024.07.14")

git_repository = use_repo_rule("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository")

# Dependencies are auto updated by a github action that
# * scans for lines that start with git_repository( or git_override(
# * inside it looks for:
#   * name/module_name
#   * remote
#   * branch
#     * this conflicts with commit in git_repository and isnt valid in git_override
#     * this line should be commented out
#   * commit
#     * due to a bit of laziness this must come last
# * checks the remote for the commit at the tip of the branch
# * if they're different, it submits a new PR for each individual dependency that needs updating

# Make sure the 4 attributes are at the top of the declaration/ahead of build_file_content.
# due to a bit of laziness in the script, the error checking only looks for build_file_content or )
# to determine end of declaration.

git_repository(
    name = "flecs",
    remote = "https://github.com/SanderMertens/flecs.git",
    # branch = "master",
    commit = "eeb9eaa64243536c5d5c19279c9c305a52d0b9b3",
    build_file_content = """
cc_library(
    name = "flecs",
    srcs = ["flecs.c"],
    hdrs = ["flecs.h"],
    features = ["treat_warnings_as_errors"],
    includes = ["."],
    visibility = ["//visibility:public"],
)
""",
)

git_repository(
    name = "unity",
    remote = "https://github.com/ThrowTheSwitch/Unity.git",
    # branch = "master",
    commit = "18fb33921f1bb973c81a9ce740c52b92a152333d",
    build_file_content = """
cc_library(
    name = "unity",
    srcs = glob(["src/*.c"]),
    hdrs = glob(["src/*.h"]),
    features = ["treat_warnings_as_errors"],
    includes = ["src/"],
    visibility = ["//visibility:public"],
)
""",
)

git_repository(
    name = "cJSON",
    remote = "https://github.com/DaveGamble/cJSON.git",
    # branch = "master",
    commit = "424ce4ce9668f288fb4ab665775546d3ed709e96",
    build_file_content = """
cc_library(
    name = "cJSON",
    srcs = ["cJSON.c"],
    hdrs = ["cJSON.h"],
    features = ["treat_warnings_as_errors"],
    includes = ["."],
    visibility = ["//visibility:public"],
)
""",
)

# Usage:
# bazel build //...
# bazel run @hedron_compile_commands//:refresh_all
bazel_dep(name = "hedron_compile_commands", dev_dependency = True)
git_override(
    module_name = "hedron_compile_commands",
    remote = "https://github.com/hedronvision/bazel-compile-commands-extractor.git",
    # branch = "main",
    commit = "1e08f8e0507b6b6b1f4416a9a22cf5c28beaba93",
)
